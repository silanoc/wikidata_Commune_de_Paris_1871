SELECT ?communard ?communardLabel ?pr_nom ?pr_nomLabel ?sexe_ou_genre ?sexe_ou_genreLabel ?date_de_naissance ?lieu_de_naissance ?lieu_de_naissanceLabel WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "fr". }
  ?communard wdt:P106 wd:Q1780490.
  OPTIONAL { ?communard wdt:P21 ?sexe_ou_genre. }
  OPTIONAL { ?communard wdt:P569 ?date_de_naissance. }
  OPTIONAL { ?communard wdt:P19 ?lieu_de_naissance. }
}

-------------------------------

SELECT ?communard ?communardLabel ?sexe_ou_genreLabel ?date_de_naissance ?lieu_de_naissanceLabel ?conjointLabel (GROUP_CONCAT(DISTINCT ?occupationLabel; SEPARATOR = ", ") AS ?LeursoccupationsLabel) (GROUP_CONCAT(DISTINCT ?prenomLabel; SEPARATOR = ", ") AS ?prenoms) ?date_de_mort ?circonstances_de_la_mortLabel ?cause_de_la_mortLabel WHERE {
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "fr".
    ?communard rdfs:label ?communardLabel.
    ?sexe_ou_genre rdfs:label ?sexe_ou_genreLabel.
    ?occupation rdfs:label ?occupationLabel.
    ?lieu_de_naissance rdfs:label ?lieu_de_naissanceLabel.
    ?conjoint rdfs:label ?conjointLabel.
    ?prenom rdfs:label ?prenomLabel.
    ?circonstances_de_la_mort rdfs:label ?circonstances_de_la_mortLabel.
    ?cause_de_la_mort rdfs:label ?cause_de_la_mortLabel.
  }
  ?communard wdt:P106 wd:Q1780490.
  OPTIONAL { ?communard wdt:P21 ?sexe_ou_genre. }
  OPTIONAL { ?communard wdt:P569 ?date_de_naissance. }
  OPTIONAL { ?communard wdt:P19 ?lieu_de_naissance. }
  OPTIONAL { ?communard wdt:P106 ?occupation. }
  OPTIONAL { ?communard wdt:P26 ?conjoint. }
  OPTIONAL { ?communard wdt:P735 ?prenom. }
  OPTIONAL { ?communard wdt:P570 ?date_de_mort. }
  OPTIONAL { ?communard wdt:P1196 ?circonstances_de_la_mort. }
  OPTIONAL { ?communard wdt:P509 ?cause_de_la_mort. }
  
}
GROUP BY ?communard ?communardLabel ?sexe_ou_genreLabel ?date_de_naissance ?lieu_de_naissanceLabel ?conjointLabel ?date_de_mort ?circonstances_de_la_mortLabel ?cause_de_la_mortLabel